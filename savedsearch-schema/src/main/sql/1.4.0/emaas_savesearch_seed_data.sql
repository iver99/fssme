Rem
Rem emaas_savesearch_seed_data.sql
Rem
Rem Copyright (c) 2013, 2014, Oracle and/or its affiliates. 
Rem All rights reserved.
Rem
Rem    NAME
Rem      emaas_savesearch_seed_data.sql 
Rem
Rem    DESCRIPTION
Rem      Seed data for category and default folder
Rem
Rem    NOTES
Rem      None
Rem
Rem    MODIFIED   (MM/DD/YY)
Rem    miayu       10/28/15 - update category name from "Target Analytics" to "Data Explorer - Search"
Rem    GUOCHEN     10/29/15 - Introduce WIDGET_SUPPORT_TIME_CONTROL for widgets
Rem

DEFINE TENANT_ID ='&1'
SET FEEDBACK ON
SET SERVEROUTPUT ON

DECLARE 
  V_CATEGORY_NAME  EMS_ANALYTICS_CATEGORY.NAME%TYPE;
BEGIN

  SELECT NAME INTO V_CATEGORY_NAME FROM EMS_ANALYTICS_CATEGORY WHERE CATEGORY_ID=2 and TENANT_ID='&TENANT_ID';
  IF (V_CATEGORY_NAME='Target Analytics') THEN
    Update EMS_ANALYTICS_CATEGORY set NAME='Data Explorer - Search' where CATEGORY_ID=2 and TENANT_ID='&TENANT_ID';
    commit;
    DBMS_OUTPUT.PUT_LINE('Category name: [Target Analytics] has been changed to [Data Explorer - Search] successfully');  
  ELSE
    DBMS_OUTPUT.PUT_LINE('Category name: [Target Analytics] has been changed to [Data Explorer - Search]  before, no need to update again');  
  END IF;
  
EXCEPTION
WHEN OTHERS THEN
  ROLLBACK;
  DBMS_OUTPUT.PUT_LINE('Failed to update Category name: [Target Analytics] to [Data Explorer - Search] due to '||SQLERRM);   
  RAISE;  
END;
/


DECLARE 
  CURSOR SEARCH_CUR IS
      SELECT SEARCH_ID, CATEGORY_ID FROM EMS_ANALYTICS_SEARCH SCH 
          WHERE DELETED=0 AND IS_WIDGET=1 AND TENANT_ID='&TENANT_ID' AND 
          NOT EXISTS (SELECT * FROM EMS_ANALYTICS_SEARCH_PARAMS PARAM 
              WHERE PARAM.SEARCH_ID=SCH.SEARCH_ID AND PARAM.PARAM_TYPE=1 AND PARAM.NAME='WIDGET_SUPPORT_TIME_CONTROL' AND PARAM.TENANT_ID='&TENANT_ID');
  SEARCH              SEARCH_CUR%ROWTYPE;
  V_COUNT             NUMBER;
  SUPPORT_TIMECONTROL VARCHAR2(2);
BEGIN
  SELECT COUNT(1) INTO V_COUNT FROM EMS_ANALYTICS_SEARCH_PARAMS WHERE TENANT_ID='&TENANT_ID' AND PARAM_TYPE=1 AND NAME ='WIDGET_SUPPORT_TIME_CONTROL';
  IF (V_COUNT>0) THEN
    DBMS_OUTPUT.PUT_LINE('WIDGET_SUPPORT_TIME_CONTROL have been updated for: &TENANT_ID, no need to update again');  
  ELSE
    OPEN SEARCH_CUR;
    LOOP
        FETCH SEARCH_CUR INTO SEARCH;
        EXIT WHEN SEARCH_CUR%NOTFOUND;
        SUPPORT_TIMECONTROL := '1';
        IF SEARCH.CATEGORY_ID = 2 THEN
          SUPPORT_TIMECONTROL := '0';
        END IF;
        INSERT INTO EMS_ANALYTICS_SEARCH_PARAMS (SEARCH_ID,NAME,PARAM_ATTRIBUTES,PARAM_TYPE,PARAM_VALUE_STR,PARAM_VALUE_CLOB,TENANT_ID) VALUES (SEARCH.SEARCH_ID, 'WIDGET_SUPPORT_TIME_CONTROL', null, 1, SUPPORT_TIMECONTROL, null, '&TENANT_ID');
    END LOOP;
    CLOSE SEARCH_CUR;
    
    COMMIT;
    DBMS_OUTPUT.PUT_LINE('Update WIDGET_SUPPORT_TIME_CONTROL in Schema object: EMS_ANALYTICS_SEARCH_PARAMS successfully for tenant: &TENANT_ID');  
  END IF;
  
EXCEPTION
WHEN OTHERS THEN
  ROLLBACK;
  DBMS_OUTPUT.PUT_LINE('Failed to update EMS_ANALYTICS_SEARCH_PARAMS.WIDGET_SUPPORT_TIME_CONTROL for tenant &TENANT_ID due to '||SQLERRM);   
  RAISE;  
END;
/
